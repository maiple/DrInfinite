// config/initialization

global.key_names = [
    "left",
    "right",
    "up",
    "down",
    "lspin",
    "rspin",
    "pause"
]

global.key_count = array_length_1d(global.key_names);

// player, key
global.key_map = [
    [
        vk_left,
        vk_right,
        vk_up,
        vk_down,
        vk_comma,
        vk_period,
        vk_enter
    ],
    [
        ord('A'),
        ord('D'),
        ord('W'),
        ord('S'),
        ord('Q'),
        ord('E'),
        vk_space
    ]
]

global.key_map = fix_array(global.key_map);

for (var i = array_height_2d(global.key_map); i < global.player_max; ++i)
{
    for (var j = 0; j < global.key_count; ++j)
    {
        global.key_map[i, j] = vk_nokey;
    }
}

global.key_down = 0;

// block colours
global.block_colours =
[
    0,
    make_color_rgb($e2, $40, $40), // red
    make_color_rgb($df, $cf, $30), //yellow
    make_color_rgb($85, $67, $f4) // purple
    //make_color_rgb($60, $ff, $40),
    //make_color_rgb($30, $70, $ff)
]

// includes empty zero-block
global.block_colours_c = array_length_1d(global.block_colours)

// a lot of arithmetic relies on this being a power of 4.
ogm_assert(global.block_colours_c == 4)

// font

global.font = font_add_sprite(sprFont, ord('0'), false, 0);
draw_set_font(global.font);

////////////////////////////////////////////////////////////////////////////////
////////////////////////////// PRIVATE DEFN ////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

#define fix_array

var arr = argument0;
var arr2 = 0;

if (array_height_2d(arr) == 1)
{
    for (var i = 0; i < array_length_1d(arr); ++i)
    {
        if (is_array(arr[@ i]))
        {
            var arri = arr[@ i]
            for (var j = 0; j < array_length_1d(arri); ++j)
            {
                arr2[i, j] = arri[@ j];
            }
        }
        else
        {
            arr2[i, 0] = arr[@ i];
        }
    }
}
else
{
    return arr2;
}

return arr2;
