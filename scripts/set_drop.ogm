/// set_drop()

ogm_assert(object_index == objGroup, "set_drop must be called from objGroup")

m_drop_x = m_width div 2
m_drop_y = m_yview - 1
m_drop_spin = 0

// determine next drop normally by consulting master list, generating if needed.
if (m_drop_index >= objControl.m_drops_list_c)
{
    var i = objControl.m_drops_list_c;

    var arr
    for (var z = 0; z < 2; ++z)
    {
        var h = irandom(global.block_colours_c - 2) + 1;
        arr[z] = h
    }
    objControl.m_drops_list[i] = arr
    objControl.m_drops_list_c++;
}

m_drop = objControl.m_drops_list[m_drop_index++]

// when y is very high, drop only single-colour blocks.
if (get_top() < -20)
{
    m_drop[1] = m_drop[0];
    m_drop_high_hysteresis ^= true;
    if (m_drop_high_hysteresis)
    {
        m_drop_high_hysteresis_type = m_drop[0];
    }
    else
    {
        m_drop[1] = m_drop_high_hysteresis_type;
        m_drop[0] = m_drop_high_hysteresis_type;
    }
}
else
{
    m_drop_high_hysteresis = false;
}

// orient drop
m_drop[0] |= K_BLOCK_ORIENTED
m_drop[1] |= K_BLOCK_ORIENTED
m_drop[0] |= K_BLOCK_ROTATION_DOWN
m_drop[1] |= K_BLOCK_ROTATION_UP

// hinge block 0
m_drop[0] |= K_BLOCK_HINGE
