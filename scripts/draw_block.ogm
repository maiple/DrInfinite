/// draw_block(x, y, type, [highlight], [outline], [transparency])
// draws a block at the given coordinates
// highlight: intended for if this is the current dropping block.
// outline: draws only the outline of the block

var btype = argument[2];
var highlight = false;
var outline = false;
var transparency = 1;
if (argument_count > 3)
{
    highlight = argument[3];
}
if (argument_count > 4)
{
    outline = argument[4];
}
if (argument_count > 5)
{
    transparency = argument[5];
}

var reps = 1;
if (outline)
{
    reps = 5;
}

// number of repetitions (only 1 if not outlining.)
for (var i = reps > 1; i < reps; ++i)
{
    // off_x, off_y are zero if not outlining..
    var off_x = (i == 1) - (i == 3);
    var off_y = (i == 2) - (i == 4);
    var _x = argument[0] + off_x;
    var _y = argument[1] + off_y;
    if (btype == "bomb" || btype == "ui-bomb")
    {
        var reps = 1;

        draw_sprite_ext(
            sprBomb, (objControl.m_round_timer div 8) mod 2,
            _x, _y, 1, 1, 0, c_white,
            // alpha
            (btype == "ui-bomb")
                ? 0.8
                : 1
        );
        exit;
    }

    ogm_assert(btype >= 0, "negative block type.")

    var colour = btype & K_BLOCK_COLOUR;

    var spr = sprBlock;
    var subimage = 0;

    if (btype & K_BLOCK_VIRUS)
    {
        spr = sprVirus;
        subimage = objControl.m_round_timer div 16 mod 4;
    }
    else if (btype & K_BLOCK_ORIENTED)
    {
        switch (btype & K_BLOCK_ROTATION)
        {
        case K_BLOCK_ROTATION_UP:
            subimage = 3;
            break;
        case K_BLOCK_ROTATION_LEFT:
            subimage = 4;
            break;
        case K_BLOCK_ROTATION_RIGHT:
            subimage = 2;
            break;
        case K_BLOCK_ROTATION_DOWN:
            subimage = 1;
            break;
        }
    }

    var _col = global.block_colours[colour]

    switch (colour)
    {
    case 0: exit;
    case 1:
    case 2:
    case 3:
        draw_sprite_ext(spr, subimage, _x, _y, 1, 1, 0, _col, transparency);
    }

    if (btype & K_BLOCK_HINGE)
    {
        draw_sprite_ext(sprBlockHinge, 0, _x, _y, 1, 1, 0, _col, transparency);

        if (highlight)
        {
            draw_sprite_ext(sprBlockHinge, 0, _x, _y, 1, 1, 0, c_white, 0.45);
        }
    }
}
